apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.fitpolo.demo.h707"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 2
        versionName "2.0"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def formatedDate = new Date().format("yyyyMMddHHmm")
                def apkFileName = "FitpoloDemoH707_${defaultConfig.versionName}_${formatedDate}.apk"
                if (outputFile.name.contains('debug')) {
                    apkFileName = "FitpoloDemoH707_${defaultConfig.versionName}_${formatedDate}_debug.apk"
                }
                outputFileName = apkFileName
            }
        }
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '28.0.0'
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.jakewharton:butterknife:7.0.1'
    implementation 'com.android.support:support-v4:28.0.0'
    // Toasty
    implementation 'com.github.GrenderG:Toasty:1.4.1'
    // constraint layout
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    // smartrefresh
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-alpha-20'
    implementation project(path: ':fitpolosupport')
}
